plugins {
    id 'net.researchgate.release' version '2.4.0'
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group 'io.saagie'
version version
description 'Gradle plugin to automatically upload archives into a specified platform and create or update jobs'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logback

    compile gradleApi()
    compile group: 'com.mashape.unirest', name: 'unirest-java', version: unirest


    testCompile group: 'junit', name: 'junit', version: jUnit
}

configurations {
    pom
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives jar, sourceJar, javadocJar
}

signing {
    required signatory != null
    sign configurations.archives
}

release {
    newVersionCommitMessage = '[skip CI] new version commit:'
}

task mergeToDevelop(type: Exec) {
    description 'Merge master to develop'
    commandLine 'git', 'merge', 'master'
}

task switchToDevelop(type: Exec) {
    description 'Checkout develop'
    commandLine 'git', 'checkout', 'develop'
}

mergeToDevelop.dependsOn switchToDevelop
updateVersion.dependsOn mergeToDevelop

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/${project.version.endsWith('-SNAPSHOT') ? 'content/repositories/snapshots' : 'service/local/staging/deploy/maven2'}") {
                authentication(userName: System.getProperty("SONATYPE_USERNAME", "admin"), password: System.getProperty("SONATYPE_PASSWORD", 'admin123'))
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description

                scm {
                    url 'scm:git@github.com:saagie/gradle-saagie-plugin.git'
                    connection 'scm:git@github.com:saagie/gradle-saagie-plugin.git'
                    developerConnection 'scm:git@github.com:saagie/gradle-saagie-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id "erwan-koffi"
                        name "Erwan Koffi"
                        url "https://github.com/erwan-koffi"
                    }
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn uploadArchives
